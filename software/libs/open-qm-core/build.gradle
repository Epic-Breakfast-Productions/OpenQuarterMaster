plugins {
    id 'java-library'
    id "io.freefair.lombok" version "6.3.0"
    id "jacoco"
    // https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:complete_example
    id 'maven-publish'
    // https://docs.gradle.org/current/userguide/signing_plugin.html#signing_plugin
//    id 'signing'
    //id 'org.kordamp.gradle.jandex' version '0.11.0'
}

group 'com.ebp.openQuarterMaster.lib'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.13.0'

    //if need ObjectIds, BsonIgnore
    implementation group: 'org.mongodb', name: 'bson', version: '4.4.0'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'


    // https://mvnrepository.com/artifact/tech.units/indriya
    //     https://unitsofmeasurement.gitbook.io/uom-guide/getting-started/getting-started-with-indriya
    implementation group: 'tech.units', name: 'indriya', version: '2.1.2'
    // https://mvnrepository.com/artifact/tech.uom.lib/uom-lib-jackson
    implementation group: 'tech.uom.lib', name: 'uom-lib-jackson', version: '2.1'
    implementation 'systems.uom:systems-common:2.1'

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '2.0.2'
    implementation 'org.passay:passay:1.6.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'


    testImplementation 'org.hibernate.validator:hibernate-validator:7.0.1.Final'
    testImplementation 'org.hibernate.validator:hibernate-validator-cdi:7.0.1.Final'

//    implementation 'org.abego.treelayout:org.abego.treelayout.core:1.0.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.8.2")
    testImplementation('org.assertj:assertj-core:3.22.0')
    implementation group: 'tech.uom.lib', name: 'uom-lib-assertj', version: '2.1'

    testImplementation group: 'com.github.javafaker', name: 'javafaker', version: '1.0.2'
    testImplementation 'ch.qos.logback:logback-classic:1.2.10'
}

test {
    useJUnitPlatform()
    reports.junitXml.enabled(true)
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'open-qm-lib-core'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Open QuarterMaster Core Library'
                description = 'A concise description of my library'
                url = 'https://github.com/Epic-Breakfast-Productions/OpenQuarterMaster/tree/main/software'
//                properties = [
//                        myProp: "value",
//                        "prop.with.dots": "anotherValue"
//                ]
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://github.com/Epic-Breakfast-Productions/OpenQuarterMaster/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'GregJohnStewart'
                        name = 'Greg Stewart'
                        email = 'contact@gjstewart.net'
                    }
                }
//                scm {
//                    connection = 'scm:git:git://example.com/my-library.git'
//                    developerConnection = 'scm:git:ssh://example.com/my-library.git'
//                    url = 'http://example.com/my-library/'
//                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

//TODO:: setup proper signing
//signing {
//    sign publishing.publications.mavenJava
//}

javadoc {
    if(JavaVersion.current().isJava11Compatible()) {
        options.addBooleanOption('html5', true)
        options.addBooleanOption('private', true)
    }
}